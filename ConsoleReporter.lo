/**
 * A test reporter that renders its output to the command line.
 *
 * If `out` is provided renders lines immediately; otherwise buffers them.
 */

 RED is (text) { reply "\x1B[38;5;1m`text`\x1B[0m"; };
 GREEN is (text) { reply "\x1B[38;5;10m`text`\x1B[0m"; };

create is (out) {
    
    report      = "";
    tests       = (passed: 0, failed: 0);
    assertions  = (passed: 0, failed: 0);
    
    // OWNER FACET
    
    reply (
        
        /**
         * Adds a test result to this report.
         */
        
        addResult: (name, passed, failed) {
                        
            summary = failed > 0 ? RED("âœ–") : GREEN("âœ”") 
                >< " `name` [`passed` of `passed + failed` passed]\n";
                        
            if out {
                out.write <- (summary);
            }
            else {
                report = report >< summary;
            }
            
            assertions.passed += passed;
            assertions.failed += failed;
            
            if failed > 0 {
                tests.failed++;
            }
            else {
                tests.passed++;
            }
        },
        
        /**
         * Finalizes this test report, returning true if all tests passed.
         */
        finalize: () {
            
            allPassed = tests.failed == 0;
            
            out.writeln <- (allPassed ? 
                GREEN("All `tests.passed` tests passed [`assertions.passed + assertions.failed` assertions]") : 
                RED("`tests.failed` tests failed [`assertions.failed` assertions failed]"));
            
            reply allPassed;
        }
    );
};