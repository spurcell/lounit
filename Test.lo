/**
 * Models a unit test.
 *
 * @param name  the name of this test
 * @param proc  the testing procedure for this test
 */
create is <-> (name, proc) {
    
    passedAssertions    = 0;
    failedAssertions    = 0;
    
    // PRIVATE METHODS
    
    /**
     * Records the result of an assertion in the test.
     *
     * @param {bool} result
     */
    record is <-> (result) {
        
        if result {
            passedAssertions++;
        }
        else {
            failedAssertions++;
        }
    };
    
    // facet to pass into test procedure
    
    procFacet = (
        
        assert: <-> (val) {
            record(val == true);
        },
        
        assertEqual: <-> (a, b) {
            record(a == b);
        }
    );

    // owner facet to run the test
    
    reply (
        
        run: <-> (report) {
            
            proc << (procFacet);
            
            report.addResult << (name, failedAssertions == 0);
        }
    );
};