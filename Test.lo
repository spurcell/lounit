/**
 * Models a unit test.
 *
 * @param name  the name of this test
 * @param proc  the testing procedure for this test
 */
create is <-> (name, proc) {
    
    passed      = true;
    assertions  = [];
    
    // PRIVATE METHODS
    
    /**
     * Records the result of an assertion in the test.
     *
     * @param {bool} result
     */
    record is <-> (result) {
        
        if result == false {
            passed = false;
        }
        
        assertions <+ result;
    };
    
    // FACET to pass into test procedure
    
    procFacet = (
        
        assert: <-> (val) {
            record(val == true);
        },
        
        assertEqual: <-> (a, b) {
            record(a == b);
        }
    );

    // FACET to run the test (returned to owner)
    
    reply (
        
        run: <-> (report) {
            
            proc << (procFacet);
            
            report.addResult << (name, passed);
        }
    );
};