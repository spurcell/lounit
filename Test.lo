/**
 * Models a unit test.
 *
 * @param name      the name of this test
 * @param runTest   the procedure to run for this test
 */
create is (name, runTest) {
    
    // count assertions
    
    numPassed = 0;
    numFailed = 0;
    
    // PRIVATE METHODS
    
    /**
     * Records the result of an assertion in the test.
     *
     * @param {bool} result
     */
    record is (result) {
        
        if result {
            numPassed++;
        }
        else {
            numFailed++;
        }
    };
    
    // facet to pass into the test procedure
    
    testFacet is (
        
        assert: (val) {
            record <- (val == true);
        },
        
        assertEqual: (a, b) {
            record <- (a == b);
        },
        
        assertFail: () {
            
            // TODO
        }
    );

    // owner facet to run the test
    
    reply (
        
        run: (report) {
            
            // run the test and collect results
            runTest <- testFacet;
            
            report.addResult <- name, numPassed, numFailed;
        }
    );
};